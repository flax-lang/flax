// gltest.flx
// Copyright (c) 2014 - 2016, zhiayang@gmail.com
// Licensed under the Apache License Version 2.0.

// import Foundation
import OpenGL.GL
import SDL2.SDL

import LibCInterface

ffi func rand() -> i32

func frand(a: f64, b: f64) -> f64
{
    let r = (rand() as f64) / 2147483647.0
    let diff = b - a

    let x = r * diff
    return a + x
}

ffi func log(a: f64) -> f64
ffi func sin(a: f64) -> f64
ffi func fabs(a: f64) -> f64
ffi func fmin(a: f64, b: f64) -> f64
ffi func fmax(a: f64, b: f64) -> f64
ffi func sqrt(a: f64) -> f64

func clamp(x: f64, lower: f64, upper: f64) -> f64
{
    return fmin(upper, fmax(x, lower))
}





public func main(argc: i32, argv: i8**) -> int
{
	SDL_Init(SDL.INIT_EVERYTHING)
	let mw = SDL_CreateWindow("test", 805240832, 805240832, 640, 480, 2)

	if mw == null
	{
		printf("omg no\n")
	}
	else
	{
		printf("window init: %p\n", mw)
	}

	let glc = SDL_GL_CreateContext(mw)

	SDL_GL_SetAttribute(SDL.GL_MULTISAMPLEBUFFERS, 1)
	SDL_GL_SetAttribute(SDL.GL_MULTISAMPLESAMPLES, 2)

	glClearColor(0, 0, 0, 0)

	glMatrixMode(GL.PROJECTION)
	glLoadIdentity()
	gluPerspective(45.0, (640.0 / 480.0), 0.1, 100.0)

	glMatrixMode(GL.MODELVIEW)
	glLoadIdentity()


	glEnableClientState(GL.VERTEX_ARRAY)
	glEnableClientState(GL.TEXTURE_COORD_ARRAY)
	glEnableClientState(GL.COLOR_ARRAY)
	glEnableClientState(GL.NORMAL_ARRAY)

	glEnable(GL.MULTISAMPLE)
	glEnable(GL.DEPTH_TEST)
	glDepthFunc(GL.LESS)

	glBlendColor(1.0, 1.0, 1.0, 1.0)

	var rx: f64 = 0
	var ry: f64 = 0
	var rz: f64 = 0

	let vertices = [	0.0, -0.525731, 0.850651,
						0.850651, 0.0, 0.525731,
						0.850651, 0.0, -0.525731,
						-0.850651, 0.0, -0.525731,
						-0.850651, 0.0, 0.525731,
						-0.525731, 0.850651, 0.0,
						0.525731, 0.850651, 0.0,
						0.525731, -0.850651, 0.0,
						-0.525731, -0.850651, 0.0,
						0.0, -0.525731, -0.850651,
						0.0, 0.525731, -0.850651,
						0.0, 0.525731, 0.850651
					]

	// var colours: f64[36]
	var colours = alloc[48] f64
	defer dealloc colours

	var i = 0
	while i < 48
	{
		colours[i + 0] = frand(0.3, 0.9)
		colours[i + 1] = frand(0.3, 0.9)
		colours[i + 2] = frand(0.3, 0.9)
		colours[i + 3] = 1.0
		i += 4
	}

	let indices: u8[60] = [
							1,  2,  6,
							1,  7,  2,
							3,  4,  5,
							4,  3,  8,
							6,  5,  11,
							5,  6,  10,
							9,  10, 2,
							10, 9,  3,
							7,  8,  9,
							8,  7,  0,
							11, 0,  1,
							0,  11, 4,
							6,  2,  10,
							1,  6,  11,
							3,  5,  10,
							5,  4,  11,
							2,  7,  9,
							7,  1,  0,
							3,  9,  8,
							4,  8,  0
						]


	let normals = [
						0.000000, -0.417775, 0.675974,
						0.675973, 0.000000, 0.417775,
						0.675973, -0.000000, -0.417775,
						-0.675973, 0.000000, -0.417775,
						-0.675973, -0.000000, 0.417775,
						-0.417775, 0.675974, 0.000000,
						0.417775, 0.675973, -0.000000,
						0.417775, -0.675974, 0.000000,
						-0.417775, -0.675974, 0.000000,
						0.000000, -0.417775, -0.675973,
						0.000000, 0.417775, -0.675974,
						0.000000, 0.417775, 0.675973
					]




	var speed = 1.0
	var scale = 1.0

	var run = true

	var srx = 0.0
	var sry = 0.0
	var srz = 0.0

	let stderr = fdopen(2, "a")
	while run
	{
		glClear(GL.COLOR_BUFFER_BIT | GL.DEPTH_BUFFER_BIT)
		glClearColor(0.109, 0.109, 0.109, 1.0)
		glTranslated(0.0, 0.0, -5.0)

		glRotated(rx, frand(0, 1), 0, 0)
		glRotated(ry, 0, frand(0, 1), 0)
		glRotated(rz, 0, 0, frand(0, 1))


		glVertexPointer(3, GL.DOUBLE, 0, vertices.data)
		glColorPointer(4, GL.DOUBLE, 0, colours.data)
		glNormalPointer(GL.DOUBLE, 0, normals.data)

		glDrawElements(GL.TRIANGLES, 60, GL.UNSIGNED_BYTE, indices as void*)

		glLoadIdentity()

		SDL_GL_SwapWindow(mw)

		usleep((1 * 1000) as u32)

		rx += srx * speed
		ry += sry * speed
		rz += srz * speed

		var e: SDL.Event
		while SDL_PollEvent(&e) == 1
		{
			if e.type == SDL.EventType.Quit
			{
				printf("\nexiting\n")
				run = false
			}
			else if e.type == SDL.EventType.KeyUp
			{
				if (&e as SDL.KeyboardEvent*).keysym.sym == SDL.Key.R
				{
					srx = frand(-2.5, 2.5)
					sry = frand(-2.5, 2.5)
					srz = frand(-2.5, 2.5)
				}
				else if (&e as SDL.KeyboardEvent*).keysym.sym == SDL.Key.F
				{
					rx = 0
					ry = 0
					rz = 0
					srx = 0
					sry = 0
					srz = 0
					speed = 1.0

					glLoadIdentity()
				}
			}
			else if e.type == SDL.EventType.KeyDown
			{
				let ke = *(&e as SDL.KeyboardEvent*)

				if ke.keysym.sym == SDL.Key.Space
				{
					srx *= 0.9
					sry *= 0.9
					srz *= 0.9

				}

				else if ke.keysym.sym == SDL.Key.C { scale -= 0.5 }
				else if ke.keysym.sym == SDL.Key.V { scale += 0.5 }
				else if ke.keysym.sym == SDL.Key.X { speed += scale }
				else if ke.keysym.sym == SDL.Key.Z { speed -= scale }
				else if ke.keysym.sym == SDL.Key.D { sry -= scale }
				else if ke.keysym.sym == SDL.Key.A { sry += scale }
				else if ke.keysym.sym == SDL.Key.Q { srz -= scale }
				else if ke.keysym.sym == SDL.Key.E { srz += scale }
				else if ke.keysym.sym == SDL.Key.W { srx -= scale }
				else if ke.keysym.sym == SDL.Key.S { srx += scale }


				fprintf(stderr, "                                               \r> %.2lf / %.2lf : %.2lf, %.2lf, %.2lf\r", speed,
					scale, srx, sry, srz)
				fflush(stderr)
			}
		}

		/*
			todo: enums suck
		*/
	}








	return 0
}










