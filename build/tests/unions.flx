// unions.flx
// Copyright (c) 2017, zhiayang
// Licensed under the Apache License Version 2.0.

export test_unions
import libc as _

union option<T>
{
	some: T
	none
}

@raw union ipv4
{
	_: struct {
		_: @raw union {
			bytes2: [u8: 4]
			raw3: u32
		}
	}

	_: struct {
		bytes: [u8: 4]
	}

	_: struct {
		raw2: u32
	}

	raw: u32
}

public fn doUnionsTest()
{
	do {

		let x = option::some("foobar")
		let y = option::some(456)

		printf("x = %s, y = %d\n", x as option!<str>::some, y as option!<int>::some)
	}

	do {
		union Bar
		{
			some: str
			other: int
			none
		}

		let q = Bar::some("hello")
		let v = Bar::other(30)
		let m = Bar::none

		printf("q = %s, v = %d\n", q as Bar::some, v as Bar::other)
	}

	do {
		var addr: ipv4
		addr.raw3 = 0xff01a8c0;
		printf("%d.%d.%d.%d\n", addr.bytes[0], addr.bytes[1], addr.bytes[2], addr.bytes[3]);
	}
}







