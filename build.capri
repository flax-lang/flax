version 1.17;
import "build/capri/Utilities.capri";

sysroot 	= File.getAbsolutePath("build/sysroot");
prefix		= "/usr/local";
outputBin	= "flaxcBin";

flaxcBin	= "{sysroot}/{prefix}/bin/{outputBin}";

project Flax default all
{
	CXX				= "clang++";
	def LLVM_CONFIG	= "llvm-config";

	target travis
	{
		LLVM_CONFIG = "llvm-config-3.5";
		Utils.println("Building for Travis CI");
	}

	CXXFLAGS	= "-g -std=gnu++1y -frtti -fexceptions -I`{LLVM_CONFIG} --includedir`";
	capriExec	= System.getExecutablePath();

	src			= "source";
	objs		= "";



	cxx_invoke			= "{CXX} {CXXFLAGS} -c -o $file$.o $file$";
	llvm_config_invoke	= "{LLVM_CONFIG} --cxxflags --ldflags --system-libs --libs core jit native bitwriter irreader";
	final_invoke		= "{CXX} {CXXFLAGS} -o '{sysroot}/{prefix}/bin/{outputBin}' ";


	target travis
	{
		System.execute(llvm_config_invoke);
	}





	// tasks
	task all depends build, buildLib;
	task build { compile(); link(); }


	task test
	{
		all();

		Utils.println("Compiling test program (build/test.flx)");
		assert System.execute("'{flaxcBin}' -dump-ir -O3 -sysroot '{sysroot}' build/test.flx") == 0;
		Utils.println("=========================");
		System.execute("build/test");
	}




	task clean
	{
		Utils.println("Cleaning");
		Utilities.executeForChanged(src, ".o", "rm $file$", true);
		File.clearModifyCache();
	}

	task compile
	{
		// System.execute("which llvm-config");
		// System.execute("which llvm-config-3.5");
		// check if any header files changed
		changed = Utilities.hasChanges("{src}/include", ".h");

		// compile them all
		assert Utilities.executeForChanged(src, ".cpp", cxx_invoke, changed);
	}

	task link
	{
		// link together
		foreach(file in File.listTree(src))
		{
			if(String.endsWith(file, ".o"))
				objs += "'{file}' ";
		}

		assert System.execute("mkdir -p '{sysroot}/{prefix}/bin'") == 0;
		assert System.execute("{final_invoke} {objs} `{llvm_config_invoke}`") == 0;

		Utils.println("Compilation complete.");
	}

	task buildLib
	{
		System.execute("mkdir -p '{sysroot}/{prefix}/lib/flaxlibs'");

		foreach(file in File.listFolder("./libs"))
		{
			file = "libs/" + file;
			if(File.isFile(file))
			{
				assert System.execute("cp {file} '{sysroot}/{prefix}/lib/flaxlibs/'") == 0;
			}
		}
	}
}






